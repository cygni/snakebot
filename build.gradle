buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'info.solidsoft.gradle.pitest:gradle-pitest-plugin:1.4.5'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.27.0'
        classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:1.2"
    }
}

plugins {
    id 'org.springframework.boot' version '2.1.3.RELEASE'
    id 'jacoco'
}

apply plugin: 'com.github.ben-manes.versions'

allprojects {
    group = 'se.cygni.snakebot'
    version = '0.1.21'

    /* Enable this for compiler warnings
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
    */
}

subprojects {

    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'eclipse'
    apply plugin: 'jacoco'
    apply plugin: 'maven-publish'

    compileJava {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
        options.encoding = 'UTF-8'
    }

    compileTestJava.options.encoding = 'UTF-8'
    javadoc.options.encoding = 'UTF-8'

    eclipse {
	    classpath {
	        downloadJavadoc = true
	        downloadSources = true
	    }
	}

    repositories {
        mavenCentral()
    }
    ext {
        springBootVersion = '2.1.3.RELEASE'
        commonsMathVersion = '3.6.1'
        commonsLangVersion = '3.9'
        commonsBeanutilsVersion = '1.9.4'
        commonsCollectionsVersion = '4.4'
        elasticSearchVersion = '2.4.0'
        guavaVersion = '28.1-jre'
        mockitoVersion = '3.2.0' //'2.25.1'
    }

    task javadocJar (type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }
    task sourceJar (type : Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives sourceJar
        archives javadocJar
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                artifactId 'snakebot-' + project.name
                from components.java
                artifact (sourceJar) {
                    classifier = 'sources'
                }
                artifact (javadocJar) {
                    classifier = 'javadoc'
                }
            }
        }
    }
    publishing {
        repositories {
            maven {
                // This is for jenkins local filesystem
                url "/var/www/mavenrepo"
            }
        }
    }

    dependencies {
    	implementation("org.apache.commons:commons-lang3:$commonsLangVersion")
	    implementation("org.apache.commons:commons-math3:$commonsMathVersion")
	    implementation("org.apache.commons:commons-collections4:$commonsCollectionsVersion")

        testImplementation('junit:junit:4.9')
        testImplementation('org.hamcrest:hamcrest:2.2')
    }

    jacocoTestReport {
	    reports {
	        xml.enabled false
	        csv.enabled false
	        html.destination file("${buildDir}/jacocoHtml")
	    }
	}
	
		tasks.withType(JavaCompile) {
	    options.compilerArgs << '-Xlint:unchecked'
	    options.deprecation = true
	}
}


