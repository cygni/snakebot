apply plugin: 'com.github.ben-manes.versions'

buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'info.solidsoft.gradle.pitest:gradle-pitest-plugin:1.1.10'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.13.0'
        classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:1.2"
    }
}

allprojects {

    group = 'se.cygni.snakebot'
    version = '0.1.19'

    /* Enable this for compiler warnings
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
    */
}

subprojects {

    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'jacoco'
    apply plugin: 'org.sonarqube'
    apply plugin: 'maven-publish'

    compileJava {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
        options.encoding = 'UTF-8'
    }

    repositories {
        mavenCentral()
    }
    ext['slf4j.version'] = '1.7.21'
    ext {
        slf4jVersion = '1.7.21'
        logbackVersion = '1.1.7'
        jacksonVersion = '2.8.2'
        springBootVersion = '1.4.0.RELEASE'
        springVersion = '4.3.2.RELEASE'
        springDataMongoVersion = '1.9.2.RELEASE'
        commonsMathVersion = '3.6.1'
        commonsLangVersion = '3.4'
        commonsBeanutilsVersion = '1.9.2'
        commonsCollectionsVersion = '4.1'
        guavaVersion = '19.0'
    }

    task javadocJar (type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }
    task sourceJar (type : Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                artifactId 'snakebot-' + project.name
                from components.java
                artifact (sourceJar) {
                    classifier = 'sources'
                }
                artifact (javadocJar) {
                    classifier = 'javadoc'
                }
            }
        }
    }
    publishing {
        repositories {
            maven {
                // This is for jenkins local filesystem
                url "/var/www/mavenrepo"
            }
        }
    }

    dependencies {
        testCompile "junit:junit:4.12"
        testCompile 'org.hamcrest:hamcrest-library:1.3'
    }

    test {
        jacoco {
            append = false
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.1'
}
